Manipulate[
 SeedRandom[sr];
 If[whichcase === 1,
  		pshort = .5;
  		plong = .3;
  		pfreedom = .2,
  		pshort = plong = pfreedom = 1/3;
         ];
 days = Table[current = 0;
   doorselect = RandomReal[];
   While[doorselect > pfreedom,
    current = If[doorselect <= (pfreedom + pshort),
      current + dshort, 
      current + dlong];
    doorselect = RandomReal[]]; 
       current,
   { i, numbertrials}
      ];
 Show[Histogram[days, 30, ImageSize -> {400, 250}, 
   AxesOrigin -> {0, 0}, PlotRange -> All, 
   AxesLabel -> {Style["hours", Italic, Blue], 
     Style["number", Italic, Blue]}, 
   PlotLabel -> 
    Style[StringForm["mean =  `` hours based on `` trials", 
      N[Mean[days]], numbertrials], Purple, FontSize -> Medium]],
  ListPlot[{{Mean[days], 0}}, PlotStyle -> {PointSize[0.03], Red}], 
  ImageSize -> {500, 300}, ImagePadding -> {{25, 25}, {25, 10}}],
 {{numbertrials, 1000, "number of trials"}, {50 -> "50", 100 -> "100",
    500 -> "500", 1000 -> "1000", 5000 -> "5000", 10000 -> "10,000", 
   20000 -> "20,000"}, Setter},
 {{whichcase, 1, 
   "probabilities"}, {1 -> 
    "\!\(\*FractionBox[\(1\), \(2\)]\) short, \
\!\(\*FractionBox[\(3\), \(10\)]\), long,\!\(\*FractionBox[\(\(\\\ \)\
\(2\)\), \(10\)]\) freedom", 
   2 -> "\!\(\*FractionBox[\(1\), \(3\)]\) short, \
\!\(\*FractionBox[\(1\), \(3\)]\) long, \!\(\*FractionBox[\(1\), \
\(3\)]\) freedom"}, Setter},
 {{dlong , 3, "hours for the long tunnel"}, 0, 25, .5, 
  Appearance -> "Labeled"},
 {{dshort, 2, "hours for the short tunnel"}, 0, 25, .5, 
  Appearance -> "Labeled"},
 {{sr, 123, "random seed"}, 1, 12345, 1},
 TrackedSymbols :> {numbertrials, whichcase, dshort, dlong, sr}, 
 SynchronousUpdating -> False]